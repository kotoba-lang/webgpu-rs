(ns kotoba.webgpu-rs.mat4-test
  (:require [clojure.test :refer [deftest is]]
            [kotoba.webgpu-rs.mat4 :as m]))

(defn- close? [a b] (< (Math/abs (- (double a) (double b))) 1e-4))
(defn- close3? [[ax ay az] [bx by bz]] (and (close? ax bx) (close? ay by) (close? az bz)))

(deftest identity-is-neutral
  (is (close3? (m/transform-point3 m/identity-mat [1.0 2.0 3.0]) [1.0 2.0 3.0])))

(deftest translation-then-scale-composition
  ;; matches kami-webgpu-rs's `model_mat_translates_lifts_and_scales`
  (let [mm (m/mul* (m/from-translation [10.0 2.0 20.0])
                    (m/from-rotation-y 0.0)
                    (m/from-scale [2.0 4.0 2.0]))
        p (m/transform-point3 mm [0.0 0.0 0.0])
        c (m/transform-point3 mm [0.5 0.0 0.0])]
    (is (close3? p [10.0 2.0 20.0]) "xz from pos, y lifted by h/2")
    (is (close? (first c) 11.0) "scaled half-extent")))

(deftest look-at-rh-faces-target
  (let [v (m/look-at-rh [0.0 0.0 5.0] [0.0 0.0 0.0] [0.0 1.0 0.0])
        p (m/transform-point3 v [0.0 0.0 0.0])]
    (is (close3? p [0.0 0.0 -5.0]) "eye maps to -z in view space when looking down -z")))

(deftest perspective-rh-projects-forward-point-inside-frustum
  (let [proj (m/perspective-rh (/ Math/PI 2.0) 1.0 0.1 100.0)
        p (m/transform-point3 proj [0.0 0.0 -1.0])]
    (is (< -1.0 (first p) 1.0))
    (is (< -1.0 (second p) 1.0))
    (is (<= 0.0 (nth p 2) 1.0) "0..1 depth range (wgpu convention)")))
