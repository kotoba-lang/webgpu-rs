(ns kotoba.webgpu-rs.geometry-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.webgpu-rs.geometry :as g]))

(deftest cube-mesh-shape
  (testing "24 verts x (pos3 + normal3), 6 faces x 2 tris x 3 indices"
    (let [[verts idx] (g/cube)]
      (is (= (count verts) (* 24 6)))
      (is (= (count idx) 36))
      (is (every? #(< % 24) idx) "all indices reference a real vertex")
      (is (= (apply max idx) 23)))))

(deftest sphere-shape
  (testing "(rings+1)*(sectors+1) verts, rings*sectors*2 tris"
    (let [rings 4 sectors 6
          [verts idx] (g/sphere 1.0 rings sectors)]
      (is (= (count verts) (* (inc rings) (inc sectors) 6)))
      (is (= (count idx) (* rings sectors 6)))
      (is (every? #(< % (/ (count verts) 6)) idx)))))

(deftest cylinder-shape
  (testing "side quads + two triangle-fan caps"
    (let [sectors 6
          [verts idx] (g/cylinder 1.0 2.0 sectors)
          ring-len (inc sectors)]
      (is (= (count verts) (* (+ (* 2 ring-len) (* 2 (inc ring-len))) 6)))
      (is (= (count idx) (* sectors 6 2)))
      (is (every? #(< % (/ (count verts) 6)) idx)))))
